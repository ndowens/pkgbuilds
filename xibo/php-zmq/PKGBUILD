# Maintainer: Nathan <ndowens@artixlinux.org>
pkgname=php-zmq
pkgver=1.1.3
pkgrel=1
pkgdesc="Software library for quick design and implment of messaged-based applications"
arch=(x86_64)
url="https://pecl.php.net/package/zmq"
license=('custom:BSD3')
depends=('php7' 'zeromq')
source=("https://pecl.php.net/get/zmq-$pkgver.tgz"
	"0001-Fix-build-for-php74.patch")
sha256sums=('c492375818bd51b355352798fb94f04d6828c6aeda41ba813849624af74144ce'
            'da60d1b748f7838a39e9c30c4c8fa93e70631879e90e1f5f40c3669640eecbc5')

prepare() {
  cd "zmq-$pkgver"
  patch -Np1 -i ../0001-Fix-build-for-php74.patch
}

build() {
  cd "zmq-$pkgver"
  /usr/bin/phpize7
  ./configure --prefix=/usr
  make
}

package() {
  cd "zmq-$pkgver"
  make INSTALL_ROOT="$pkgdir/" install
}
